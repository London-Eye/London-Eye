title: CompletaElCamino-PreGame
tags: Distribute
---
<<if $FirstTime>>
	[[CompletaElCamino-Dialogue]]
<<else>>
	<color=orange>Gira las partes de los caminos para que la suposición inicial (una bolita) los atraviese y llegue desde el cerebro hasta la bombilla.</color>
<<endif>>
===



title: CompletaElCamino-Dialogue
tags: PreGame
---
<<set $CompletaCamino to $CompletaCamino+1>>
<<if $CompletaCamino eq 1>>
	<color=lightblue>Deiv I.D.:</color> Correos con la víctima, redes sociales, etc.
	<color=lightblue>Deiv I.D.:</color> He encontrado bastante información sobre {$SuspectName}.
	<color=lightblue>Deiv I.D.:</color> Por desgracia, a simple vista, nada de esto me revela nada.
	<color=lightblue>Deiv I.D.:</color> Es todo superficial y no puedo sacar nada en claro.
	<color=lightblue>Deiv I.D.:</color> Tal vez, juntándolo todo, y mirándolo con perspectiva, pueda encontrar algo.
	<color=lightblue>Deiv I.D.:</color> Me va a llevar mi tiempo, espero que valga la pena.
<<elseif $CompletaCamino eq 2>>
	<color=lightblue>Deiv I.D.:</color> Otro aparente callejón sin salida, pero tengo que seguir investigando a {$SuspectName}.
	<color=lightblue>Deiv I.D.:</color> Tendré que volver a sacar a relucir mis famosas dotes deductivas.
	<color=lightblue>Deiv I.D.:</color> Esta vez he podido sacar algo más de infromación sobre sus familiares.
	<color=lightblue>Deiv I.D.:</color> Vamos a ver si tengo algo de suerte.
<<elseif $CompletaCamino eq 3>>
	<color=lightblue>Deiv I.D.:</color> ¿En serio? ¿Tampoco encuentro más información sobre {$SuspectName}?
	<color=lightblue>Deiv I.D.:</color> Supongo que tendré que volver a revisar todo lo que he obtenido sobre [select {$SuspectIsMale} True="él" False="ella"].
	<color=lightblue>Deiv I.D.:</color> Esto de no poder salir a la calle a investigar me está matando.
<<endif>>

<color=lightblue>Deiv I.D.:</color> <color=silver>-Dándose palmaditas en la cara-</color> Vamos, <color=lightblue>Deiv</color>, que tú puedes.

<color=orange>Gira las partes de los caminos para que la suposición inicial (una bolita) los atraviese y llegue desde el cerebro hasta la bombilla.</color>
===



title: CompletaElCamino-PostGame
tags: PostGame
---
<<CompletePuzzle GameManager>>


<<if $CompletaCamino eq 1>>
	<color=lightblue>Deiv I.D.:</color> Curiosamente estaba más a la vista de lo que esperaba.
<<elseif $CompletaCamino eq 2>>
	<color=lightblue>Deiv I.D.:</color> ¡Al fin! después de todo este trabajo, es bueno llegar a una conclusión.
<<elseif $CompletaCamino eq 3>>
	<color=lightblue>Deiv I.D.:</color> Claramente soy un genio, nadie más podría haber encontrado algo útil entre tanta información.
<<endif>>

<<if $SuspectEvidencesFound eq 0>>
	<color=lightblue>Deiv I.D.:</color> A ver qué he sacado en claro....
	<color=lightblue>Deiv I.D.:</color> Es una persona misteriosa, por algo me ha costado tanto encontrar cosas.
	<color=lightblue>Deiv I.D.:</color> Pero después de todo el esfuerzo puedo decir que {$SuspectName} es <b>{$SuspectRelation}</b> de la víctima.
	<<set $SuspectEvidencesFound to $SuspectEvidencesFound + 1>>
<<elseif $SuspectEvidencesFound eq 1>>
	<color=lightblue>Deiv I.D.:</color> Resulta que la respuesta estaba delante mía después de todo.
	<color=lightblue>Deiv I.D.:</color> Estas fotos junto a la víctima y sus conversaciones demuestran que {$SuspectName} <b>{$SuspectEmotion}</b> la víctima.
	<<set $SuspectEvidencesFound to $SuspectEvidencesFound + 1>>
<<elseif $SuspectEvidencesFound eq 2>>
	<color=lightblue>Deiv I.D.:</color> Después de revisar publicaciones, horas y lugares, está todo claro.
	<color=lightblue>Deiv I.D.:</color> Una vez cotejado con el crimen, resulta claro que {$SuspectName} <b>{$SuspectHasAlibiAsString}</b> tiene una coartada válida para el crimen.
	<<set $SuspectEvidencesFound to $SuspectEvidencesFound + 1>>
<<endif>>

<<set $PuzzlesResueltos to $PuzzlesResueltos+1>>
<<set $FromPuzzle to true>>
<<BackToMainMenu GameDialogue>>
===