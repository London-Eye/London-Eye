title: InicioPartida
tags: Inicio
---
<<StartNewGame GameManager>>
<color=lightblue>???:</color> <color=silver>(Soy Deiv I.D., un aclamado investigador que trabaja para la policía)</color>
<color=lightblue>Deiv I.D.:</color> <color=silver>(Normalmente me envían a resolver los casos más difíciles, esos que la policía no puede resolver)</color>
<color=lightblue>Deiv I.D.:</color> <color=silver>(En este último caso me han enviado a Londres)</color>
<color=lightblue>Deiv I.D.:</color> <color=silver>(Parece un caso cualquiera que la policía debería poder resolver, pero hay algo más)</color>
<color=lightblue>Deiv I.D.:</color> <color=silver>(La ciudad se ha visto azotada por una pandemia)</color>
<color=lightblue>Deiv I.D.:</color> <color=silver>(Lo que significa que la policía no tiene recursos suficientes ahora mismo)</color>
<color=lightblue>Deiv I.D.:</color> <color=silver>(Y por si fuera poco, la ciudad está en cuarentena, por lo que no puedo salir a investigar)</color>
<color=maroon>Teléfono:</color> *Sonidos de vibración*
<color=silver>-Descolgando el teléfono-</color> <color=lightblue>Deiv I.D.:</color> Sí, dígame.
<color=maroon>Teléfono:</color> ¿<color=lightblue>Deiv</color>? Tenemos que hablar sobre el caso.
<color=lightblue>Deiv I.D.:</color> <color=silver>(La chica que llama es </color><color=magenta>Diana</color><color=silver>, mi jefa)</color> Por fin, ya era hora de que me informaras de algo.
<color=magenta>Diana:</color> [select {$VictimIsMale} True="El chico" False="La chica"] que ha muerto se llamaba {$VictimName}.
<color=magenta>Diana:</color> Trabajaba en el supermercado como [select {$VictimIsMale} True="reponedor" False="reponedora"] y [select {$VictimIsMale} True="lo" False="la"] mataron días antes del confinamiento.
<color=lightblue>Deiv I.D.:</color> <color=silver>-Tomando apuntes en un diario-</color> ¿Dónde [select {$VictimIsMale} True="lo" False="la"] mataron? ¿En el propio supermercado?
<color=magenta>Diana:</color> No, la escena del crimen es su casa, [select {$VictimIsMale} True="lo" False="la"] mataron allí.
<color=lightblue>Deiv I.D.:</color> <color=silver>-Terminando de tomar apuntes-</color> De acuerdo. ¿Algo más, jefa?
<color=magenta>Diana:</color> Sí, el departamento te enviará una lista de sospechosos iniciales; a partir de ahí, empezará tu trabajo.
<color=lightblue>Deiv I.D.:</color> <color=silver>-Cerrando el diario-</color> Yo me ocupo jefa, como hago siempre.
<color=magenta>Diana:</color> Ten en cuenta que este es un caso especial.
<color=magenta>Diana:</color> La gente tiene los nervios a flor de piel y nosotros no tenemos recursos para investigar estas cosas.
<color=magenta>Diana:</color> Además, los conservadores presionan al gobierno para que termine la cuarentena.
<color=magenta>Diana:</color> Que pasara impune un crimen hacia [select {$VictimIsMale} True="un trabajador" False="una trabajadora"] esencial podría crear más caos del que ya hay.
<color=magenta>Diana:</color> Por si fuera poco, tenemos a la prensa echándose a nuestros cuellos.
<color=magenta>Diana:</color> Resuelve esto, y resuélvelo rápido <color=lightblue>Deiv</color>, solo tenemos una oportunidad.
<color=lightblue>Deiv I.D.:</color> Lo sé jefa, déjalo en mis manos.
<color=maroon>Teléfono:</color> <color=silver>-Cuelga-</color>
<color=lightblue>Deiv I.D.:</color> Manos a la obra.
===





title: MainMenu-Return
tags: Return
---
<<set $NinePuzzle to 0>>
<<set $CompletaCamino to 0>>
<<set $MoveTheBlock to 0>>
<<set $SeparaColores to 0>>
<<set $PuzzlesResueltos to 0>>
<color=lightblue>Deiv I.D.:</color> Probablemente debería empezar investigando a los sospechosos.
<color=lightblue>Deiv I.D.:</color> Aunque no estaría de más saber algo más sobre la víctima.
<color=orange>Puedes empezar a familiarizarte con el juego dándole a la imagen de</color> <color=lightblue>Deiv I.D.</color>
===





title: Policia-PistasJuego
tags: PistasJuego
---
<<set $AllowSkip to true>>
<<set $ContinueMode to "skip">>
<color=lightblue>Deiv I.D.:</color> <color=silver>-Abriendo su diário personal-</color> ¿Sobre qué deberia consultar información en mis notas?
<<set $ContinueMode to "time">>
->Sobre la funcionalidad de algo en la oficina.
    [[Pistas-MenuPrincipal]]
->Sobre acusar a uno de los sospechosos.
    [[Pistas-AcusarSospechoso]]
->Sobre alguna información útil en general.
    [[Pistas-General]]
->Nada, deberia ponerme a investigar.
===

title: Pistas-MenuPrincipal
tags: MenuPrincipal
---
<<set $RandomIntMin to 0>>
<<set $RandomIntMax to 4>>
<<set $CurrentRandomInt to $RandomIntPool>>

<<if $CurrentRandomInt eq 0>>
    <color=lightblue>Deiv I.D.:</color> Puedo acceder a los sospechosos desde el tablón de la pared.
<<elseif $CurrentRandomInt eq 1>>
    <color=lightblue>Deiv I.D.:</color> En el archivador puedo encontrar los puzzles que te he dejado a medias.
<<elseif $CurrentRandomInt eq 2>>
    <color=lightblue>Deiv I.D.:</color> En el ordenador de la derecha puedo llevar a juicio al sospechoso que creo que es el asesino.
<<elseif $CurrentRandomInt eq 3>>
    <color=lightblue>Deiv I.D.:</color> En el Walkie-Talkie que está encima del archivador puedo encontrar información útil sobre la víctima y la escena del crimen.
<<endif>>
===

title: Pistas-AcusarSospechoso
tags: AcusarSospechoso
---
<<set $RandomIntMin to 0>>
<<set $RandomIntMax to 3>>
<<set $CurrentRandomInt to $RandomIntPool>>

<<if $CurrentRandomInt eq 0>>
    <color=lightblue>Deiv I.D.:</color> Si acuso a un sospechoso con menos de 2 pistas es muy probable que no den el juicio como válido.
<<elseif $CurrentRandomInt eq 1>>
    <color=lightblue>Deiv I.D.:</color> Solo puedo acusar a un sospechoso, tendré que elegir con cuidado.
<<elseif $CurrentRandomInt eq 2>>
    <color=lightblue>Deiv I.D.:</color> Si he investigado todo lo posible, y he elegido al sospechoso por descarte, es posible que den el juicio como válido.
<<endif>>
===

title: Pistas-General
tags: General
---
<<set $RandomIntMin to 0>>
<<set $RandomIntMax to 4>>
<<set $CurrentRandomInt to $RandomIntPool>>

<<if $CurrentRandomInt eq 0>>
    <color=lightblue>Deiv I.D.:</color> Si me atasco en un puzzle puedo dejarlo para más tarde. Pero no podré seguir investigando a ese sospechoso hasta que lo complete.
<<elseif $CurrentRandomInt eq 1>>
    <color=lightblue>Deiv I.D.:</color> Si no recojo información sobre la víctima, las pistas que consiga me servirán de poco.
<<elseif $CurrentRandomInt eq 2>>
    <color=lightblue>Deiv I.D.:</color> Puedo acelerar el texto con <color=orange>Espacio</color> o saltarlo con <color=orange>Enter</color>.
<<elseif $CurrentRandomInt eq 3>>
    <color=lightblue>Deiv I.D.:</color> Tengo un número limitado de investigaciones sobre los sospechosos, tendré con cuidado.
<<endif>>
===





title: Walkie-InvestigarVictima
tags: InvestigarVictima
---
<<set $AllowSkip to true>>
<<set $ContinueMode to "skip">>
<color=cyan>Policía:</color> Buenas señor <color=lightblue>I.D.</color>. ¿Que quiere saber sobre el crímen?
<<set $ContinueMode to "time">>
->¿Cómo se encuentra el cuerpo?
    [[Walkie-Relacion]]
->¿Cómo murió la víctima?
    [[Walkie-Emocion]]
->Ahora mismo nada, gracias.
    <color=cyan>Policía:</color> Llámenos si necesita algo.
===

title: Walkie-Relacion
tags: Relacion
---
<<if $CriminalRelationIndexCriminal < 4>>
    <color=cyan>Policía:</color> La víctima parece estar relajada y tranquila. Probablemente, quien [select {$VictimIsMale} True="lo" False="la"] mató fue algún conocido cercano de [select {$VictimIsMale} True="este" False="esta"].
<<elseif $CriminalRelationIndexCriminal > 3>>
    <color=cyan>Policía:</color> Aunque no parece que se lo viera venir, ya que no hay signos de que se haya defendido, la víctima no está relajada del todo así que en la comisaría pensamos que quien mató a la víctima no era cercano a esta.
<<endif>>
===

title: Walkie-Emocion
tags: Emocion
---
<<if $CriminalEmotionIndexCriminal is 0 or $CriminalEmotionIndexCriminal is 1>>
    <color=cyan>Policía:</color> La víctima murió por múltiples contusiones en la parte posterior de la cabeza. Quien lo realizó probablemente tenía sentimientos negativos muy fuertes hacia [select {$VictimIsMale} True="él" False="ella"].
<<elseif $CriminalEmotionIndexCriminal is 2 or $CriminalEmotionIndexCriminal is 3>>
    <color=cyan>Policía:</color> Mataron a la víctima de un tiro en la cabeza desde la nuca. Quien [select {$VictimIsMale} True="lo" False="la"] mató seguramente tenía sentimientos negativos hacia la víctima, pero no suficientemente fuertes como para ensañarse.
<<elseif $CriminalEmotionIndexCriminal is 4 or $CriminalEmotionIndexCriminal is 5>>
    <color=cyan>Policía:</color> La víctima tenia un cuchillo clavado en el corazón, esa era su única herida. Lo más seguro es que quien lo hizo sentía algo por la víctima.
<<elseif $CriminalEmotionIndexCriminal is 6 or $CriminalEmotionIndexCriminal is 7>>
    <color=cyan>Policía:</color> El motivo de la muerte es una sola contusión en la cabeza. Parece más un accidente que un asesinato premeditado.
<<endif>>
===





title: Puzzle-Limit-Exceeded
tags: Warning
---
<color=lightblue>Deiv I.D.:</color> Parece que me he quedado sin tiempo, la prensa está presionando al departamento.
<color=lightblue>Deiv I.D.:</color> Debería revisar las pruebas que he obtenido.
<color=lightblue>Deiv I.D.:</color> Con esto, además de la información sobre la víctima y la escena del crímen, debería ser capaz de realizar una acusación.
<color=lightblue>Deiv I.D.:</color> Debería volver a echarle un vistazo a esa información. Por si a caso.
<color=lightblue>Deiv I.D.:</color> Puede que llame a la comisaria con el Walkie para que me refresquen la memória.
===




title: MainMenu-Back
tags:Back
---
<<if $FromPuzzle>>
    <<if $PuzzlesResueltos eq 3>>
        <color=maroon>Teléfono:</color> <color=silver>*Sonidos de vibración*</color>
        <color=silver>-Descolgando el teléfono-</color> <color=lightblue>Deiv I.D.:</color> Sí, dígame.
        <color=magenta>Diana:</color> <color=lightblue>Deiv</color>, ¿como llevas el caso?
        <color=lightblue>Deiv I.D.:</color> Bastante bien jefa.
        <color=lightblue>Deiv I.D.:</color> Acabo de terminar de investigar tres fuentes de información
        <color=magenta>Diana:</color> Me alegro que vaya bien.
        <color=magenta>Diana:</color> Recuerda que estamos en una pandémia así que no puedes salir de casa.
        <color=lightblue>Deiv I.D.:</color> Si jefa, lo tengo en cuenta.
        <color=maroon>Teléfono:</color> <color=silver>-Cuelga-</color>
    <<elseif $PuzzlesResueltos eq 6>>
        <color=maroon>Teléfono:</color> *Sonidos de vibración*
        <color=silver>-Descolgando el teléfono-</color> <color=lightblue>Deiv I.D.:</color> Sí, dígame.
        <color=magenta>Diana:</color> <color=lightblue>Deiv</color>, ¿estas avanzando?
        <color=lightblue>Deiv I.D.:</color> Si, jefa, poco a poco.
        <color=magenta>Diana:</color> Pues date prisa, tenemos una fecha máxima, recuérdalo.
        <color=lightblue>Deiv I.D.:</color> Lo recuerdo, sin problema.
        <color=maroon>Teléfono:</color> <color=silver>-Cuelga-</color>
    <<elseif $PuzzlesResueltos eq 9>>
        <color=maroon>Teléfono:</color> *Sonidos de vibración*
        <color=silver>-Descolgando el teléfono-</color> <color=lightblue>Deiv I.D.:</color> Sí, dígame.
        <color=magenta>Diana:</color> <color=lightblue>Deiv</color>, espero resultados.
        <color=lightblue>Deiv I.D.:</color> Estoy sobre la pista pero necesito más tiempo.
        <color=magenta>Diana:</color> Aun te queda algo de tiempo pero se te termina.
        <color=magenta>Diana:</color> La próxima vez que te llame será la última así que apresúrate.
        <color=lightblue>Deiv I.D.:</color> De acuerdo.
        <color=maroon>Teléfono:</color> <color=silver>-Cuelga-</color>
    <<elseif $PuzzlesResueltos eq 12>>
        <color=maroon>Teléfono:</color> *Sonidos de vibración*
        <color=silver>-Descolgando el teléfono-</color> <color=lightblue>Deiv I.D.:</color> Sí, dígame.
        <color=magenta>Diana:</color> <color=lightblue>Deiv</color>, como te dije, ya no hay tiempo.
        <color=magenta>Diana:</color> Tienes que presentar a juicio a uno de los sospechosos.
        <color=magenta>Diana:</color> A no ser que no hayas podido resolver el caso.
        <color=lightblue>Deiv I.D.:</color> Tranquila jefa, lo he resuelto.
        <color=lightblue>Deiv I.D.:</color> Dame un poco de tiempo para ordenar la información y enviaré a alguno a juicio.
        <color=magenta>Diana:</color> Te esperaré en el juzgado.
        <color=lightblue>Deiv I.D.:</color> Allí estaré.
        <color=lightblue>Deiv I.D.:</color> <color=silver>-Colgando el teléfono-</color> Hora de ir al ordenador para acusar a alguien.
    <<endif>>
<<endif>>
<<set $FromPuzzle to false>>
===
